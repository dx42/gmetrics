plugins {
    id 'groovy'
    id 'maven'
    id 'signing'
    id 'net.saliman.cobertura' version '2.5.0'
}

group = 'org.gmetrics'
version = '1.0'

description = 'GMetrics'

sourceCompatibility = 1.6
targetCompatibility = 1.6

tasks.withType(JavaCompile) { options.encoding = 'UTF-8' }

repositories {
    maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
    maven { url "http://repo.maven.apache.org/maven2" }
}

dependencies {
    def groovyVersion = "2.4.12"
    def slf4jVersion = "1.7.25"

    compile "org.codehaus.groovy:groovy-all:$groovyVersion"
    compile "org.codehaus.groovy:groovy-ant:$groovyVersion"
    compile "org.slf4j:slf4j-api:$slf4jVersion"

    testCompile 'junit:junit:4.12'
    testCompile "org.slf4j:slf4j-simple:$slf4jVersion"
    testCompile group:'org.codenarc', name:'CodeNarc', version:'0.27.0', transitive:false

    // TODO Remove once CodeNarc removes Log4J dependency
    testCompile "org.slf4j:log4j-over-slf4j:$slf4jVersion"
}


task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

javadoc {
    if (JavaVersion.current().java8Compatible) {
        options.addStringOption('Xdoclint:none', '-quiet')
    }
}

// Publish to Sonatype (OSSRH) Maven Repository
// See http://central.sonatype.org/pages/gradle.html
uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { deployment -> signing.signPom(deployment) }

            def repoUser = hasProperty("ossrhUsername") ? ossrhUsername : 'NotSet'
            def repoPassword = hasProperty("ossrhPassword") ? ossrhPassword : 'NotSet'
            
            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: repoUser, password: repoPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: repoUser, password: repoPassword)
            }

            pom.project {
                name 'GMetrics'
                packaging 'jar'
                // optionally artifactId can be defined here
                description 'The GMetrics project provides calculation and reporting of size and complexity metrics for Groovy source code.'
                url 'http://codenarc.org'

                scm {
                    connection 'scm:git:git@github.com:dx42/gmetrics.git'
                    developerConnection 'scm:git:git@github.com:dx42/gmetrics.git'
                    url 'scm:git:git@github.com:dx42/gmetrics.git'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'chrismair'
                        name 'Chris Mair'
                        email 'chrismair@users.sourceforge.net'
                    }
                }
            }
        }
    }
}

//release {
//    tagPrefix = project.name
//    requireBranch = '' // disabled
//}
