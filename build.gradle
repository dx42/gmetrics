plugins {
    id 'groovy'
    id 'maven-publish'
    id 'signing'
}

group = 'org.gmetrics'
archivesBaseName = 'GMetrics'
version = '2.1.0'

description = 'GMetrics'

sourceCompatibility = 1.8
targetCompatibility = 1.8

tasks.withType(JavaCompile) { options.encoding = 'UTF-8' }

repositories {
    maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
    maven { url "https://repo.maven.apache.org/maven2" }
}

dependencies {
    def groovyVersion = "3.0.9"
    def slf4jVersion = "1.7.32"
    def junitVersion = "5.8.2"

    implementation "org.codehaus.groovy:groovy:$groovyVersion"
    implementation "org.codehaus.groovy:groovy-xml:$groovyVersion"
    implementation "org.codehaus.groovy:groovy-ant:$groovyVersion"
    implementation "org.codehaus.groovy:groovy-templates:$groovyVersion"

    implementation "org.slf4j:slf4j-api:$slf4jVersion"

    testImplementation(platform("org.junit:junit-bom:${junitVersion}"))
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine")

    testImplementation "org.slf4j:slf4j-simple:$slf4jVersion"
    testImplementation 'org.codenarc:CodeNarc:3.0.1'
}

test {
    useJUnitPlatform()
}

java {
    withJavadocJar()
    withSourcesJar()
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

javadoc {
    if (JavaVersion.current().java8Compatible) {
        options.addStringOption('Xdoclint:none', '-quiet')
    }
}

// Publish to Sonatype (OSSRH) Maven Repository
// See http://central.sonatype.org/pages/gradle.html
publishing {
    publications {
        maven(MavenPublication) {
            from components.java

            artifactId = 'GMetrics'

            pom {
                name = 'GMetrics'
                description = 'The GMetrics project provides calculation and reporting of size and complexity metrics for Groovy source code.'
                url = 'http://gmetrics.org'

                scm {
                    connection = 'scm:git:git@github.com:dx42/gmetrics.git'
                    developerConnection = 'scm:git:git@github.com:dx42/gmetrics.git'
                    url = 'scm:git:git@github.com:dx42/gmetrics.git'
                }

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id = 'chrismair'
                        name = 'Chris Mair'
                        email = 'chrismair@users.sourceforge.net'
                    }
                }
            }
        }
    }

    repositories {
        maven {
            def releasesUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
            def snapshotsUrl = 'https://oss.sonatype.org/content/repositories/snapshots/'
            url = version.endsWith('SNAPSHOT') ? snapshotsUrl : releasesUrl
            credentials {
                username = ossrhUsername
                password = ossrhPassword
            }
        }
    }

}
